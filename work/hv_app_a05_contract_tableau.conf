sql="
use ${HIVE_DB_APP};
set hive.exec.dynamic.partition.mode=nonstrict ;
with  saledata  as
(select a.the_date
      ,b.projectguid
      ,b.contractno
      ,sum(case when b.the_date=a.the_date then b.saleamt else 0 end )  saleamt
      ,sum(case when b.the_date=a.the_date then b.salenum else 0 end )  salenum
      ,sum(case when b.the_date>=concat(substr(a.the_date,1,7),'-01')
                 and b.the_date <= a.the_date
                 then b.saleamt else 0 end ) saleamt_y
      ,sum(case when b.the_date>=concat(substr(a.the_date,1,7),'-01')
                 and b.the_date <= a.the_date
                 then b.salenum else 0 end ) salenum_y

from (select the_date from ${HIVE_DB_DIM}.pub_longfor_date
      where the_date>='2017-01-01'
      ) a
left join (
SELECT
a.the_date
,a.projectguid
,a.contractno
,SUM(a.saleamt)    saleamt
,sum(a.salenum)    salenum
from ${HIVE_DB_DWS}.f05_longfor_saledata a
inner  join  ${HIVE_DB_DWS}.f05_contract  b
on a.the_date=b.the_date
and a.contractno=b.contractno  and a.projectguid=b.projectguid
where b.rentaltype in  ('Main','SingleCabinet','Site','SubAnchor','Exclusive')
GROUP BY
a.the_date
,a.projectguid
,a.contractno
) b
on 1=1
group by
       a.the_date
      ,b.projectguid
      ,b.contractno
)

insert overwrite table a05_contract_tableau partition(date_key)
select a.the_date
       ,a.projectguid
       ,b.projectname
       ,b.companyguid
       ,b.companyname
       ,b.city
       ,b.province
       ,b.district_name
       ,case when substr(b.projectopen_date,1,10) > a.the_date and
                  add_months(a.the_date,12)>=substr(b.projectopen_date,1,10)  then 1
              when  a.the_date>= substr(b.projectopen_date,1,10) then  0
              else 2 end   as  preparatory_period
       ,b.is_ziguan
       ,b.is_finance
       ,a.contractno
       ,a.contractid
       ,case when datediff(a.the_date,c.open_date) <30 then 1 else 0 end  as is_open
       ,c.open_date
       ,datediff(a.the_date,c.open_date)  as  open_days
       ,a.rentaltype
       ,d.formatid_level1
       ,d.formatname_level
       ,d.formatid_level2
       ,d.formatname_level2
       ,d.formatid_level3
       ,d.formatname_level3
       ,c.branddesc
       ,e.saleamt  as saleamt
       ,f.saleamt  as last_week_sale_amt
       ,e.saleamt_y as saleamt_m_sum
       ,e.salenum   as salenum
       ,f.salenum   as last_week_salenum
       ,e.salenum_y as salenum_m_sum
       ,c.totalarea
       ,from_unixtime(unix_timestamp(), 'yyyy-MM-dd HH:mm:ss')       as op_time                   --时间戳
       ,'${EXECUTION_ID}'                                            as execution_id              --批次号
       ,'${CURRENT_FLOW_START_DAY}'                                  as load_date                 --入库时间
       ,substr(a.the_date,1,7)    as date_key
from
    ${HIVE_DB_DWS}.f05_contract  a
left join ${HIVE_DB_DIM}.pub_longfor_project_temp_sysmartbi b
      on a.projectguid=b.projectguid
left join ${HIVE_DB_DIM}.d05_longfor_contract c
      on c.contract_id=a.contractid
left join ${HIVE_DB_DIM}.d05_longfor_format d
      on d.format_id=c.main_formatid
left join   saledata  e
         on e.contractno=a.contractno and a.projectguid=e.projectguid and e.the_date =a.the_date
left join saledata  f
          on e.the_date=date_sub(f.the_date,-7) and e.contractno = f.contractno and a.projectguid=f.projectguid
where  a.rentaltype  in  ('Main','SingleCabinet','Site','SubAnchor','Exclusive')
;
"
task_incre_type=multi-day







