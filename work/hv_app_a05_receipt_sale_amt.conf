sql="
-- 修改日期：2019-04-15
-- 版本号：1.37
-- 修改人：张朋飞
-- 变更内容：经营日报新增车位周转率指标，添加车位数与车流数字段。

use ${HIVE_DB_APP};
set hive.exec.dynamic.partition.mode=nonstrict ;
with dim_main as (
select
a.the_date
,c.companyguid
,c.companyname
,c.projectguid
,c.projectname
,sum(b.jfmoney) as jfmoney
,sum(b.gxmoney) as gxmoney
,sum(b.jfcount) as jfcount
,sum(b.gxcount) as gxcount
,c.city
,c.province
,c.district_name
,sum(d.amt_sum) as amt_sum  --当日金额
,sum(d.num_sum) as num_sum
,sum(e.bonus_asc) as bonus_asc
,sum(f.bonus_inc) as bonus_inc
,sum(f.member_saleamt) as member_saleamt
,sum(f.member_salenum) as member_salenum
,sum(h.customer) as customer  --客流
,sum(k.project_building_area ) as project_building_area --建筑面积
,sum(h.customer/k.project_building_area )  as customer_density  --客流密度
,k.projectopen_date as projectopen_date   --开业时间
,c.is_ziguan  as is_ziguan  --是否资管统计项目
,c.is_finance as is_finance  --是否财务统计项目
,sum(k.crent_area) as crent_area --可出租面积
,sum(k.rentarea)  as rentarea  --已出租面积
,sum(k.openarea)  as openarea --开业面积
,sum(k.jparea) as jparea --接铺面积
,sum(t.rent_area_bd)  as rent_area_bd --月预算可租面积
,sum(t.sale_amt)  as sale_amt --月预算销售金额
,sum(t.sale_amt/t.rent_area_bd) as budget_pingxiao --月预算坪效
,sum(t.average_passenger_flow_bd) as average_passenger_flow_bd --日预算客流
,sum(t.average_passenger_flow_bd) * cast(substr(a.the_date,9,10) as int) as month_budget_insum  --月预算客流
from
(select
the_date
from ${HIVE_DB_DIM}.dim_date
where year(the_date)>=year('${START_DATEKEY}')-1
and  the_date<'${END_DATEKEY}'
group by the_date)a
left join ${HIVE_DB_DIM}.pub_longfor_project_temp_sysmartbi c  on 1=1
left join ${HIVE_DB_DWS}.f05_receipt b
on b.project_id=c.projectguid and b.the_date=a.the_date
left join
(
select substr(a.the_date,1,10) as the_time,a.projectguid, sum(a.saleamt) as amt_sum,sum(a.salenum) as num_sum
from ${HIVE_DB_DWS}.f05_longfor_saledata a
left join ${HIVE_DB_DWS}.f05_contract b
on substr(a.the_date,1,10)=substr(b.the_date,1,10) and  a.contractno=b.contractno
 and  a.projectguid=b.projectguid
 where b.rentaltype in ('Main','SingleCabinet','Site','SubAnchor','Exclusive')
group by substr(a.the_date,1,10),a.projectguid
)d
on substr(a.the_date,1,10)=d.the_time and c.projectguid=d.projectguid
left join (
select
a.the_date,a.project_guid
,sum(a.bonus_asc)  as bonus_asc
from(
select the_date,project_guid,sum(bonus_amt)*-1 as bonus_asc from ${HIVE_DB_DWS}.f05_longfor_member_sale
where adjustdesc is not null and entryby like '%WeChat%' and sale_amt=0 and bonus_amt<0
group by the_date,project_guid
union all
select  the_date,projectguid as  project_guid ,sum(exchangebonus) as bonus_asc    from  ${HIVE_DB_DWS}.f05_bonus_pay group by the_date,projectguid
union all  select the_date,project_guid,sum(redeem_amt) as bonus_asc    from  ${HIVE_DB_DWS}.f05_longfor_member_redeem group by the_date,project_guid)a
group by a.the_date,a.project_guid
)e
on substr(a.the_date,1,10)=e.the_date and c.projectguid=e.project_guid
left join (
select the_date,project_guid,sum(bonus_amt) as bonus_inc,count(1)  as member_salenum,sum(sale_amt)  as member_saleamt   from ${HIVE_DB_DWS}.f05_longfor_member_sale
where !(adjustdesc is not null and entryby like '%WeChat%' and sale_amt=0 and bonus_amt<0)
group by the_date,project_guid
)f
on substr(a.the_date,1,10)=f.the_date and c.projectguid=f.project_guid
left join (
select  the_date,project_guid,sum(in_sum) as customer from ${HIVE_DB_DWS}.f05_longfor_customer group by the_date,project_guid
)h
on substr(a.the_date,1,10)=h.the_date and c.projectguid=h.project_guid
left join (select projectguid,the_date,crent_area,rentarea,openarea,jparea,project_building_area,projectopen_date from ${HIVE_DB_DWS}.f05_project where version_code='initversion') k
on substr(a.the_date,1,10)=k.the_date and c.projectguid=k.projectguid
left join (
select the_month,projectguid,sum(rent_area_bd) as rent_area_bd,sum(main_rent_income)+sum(other_rent_income) as sale_amt,sum(average_passenger_flow_bd) as average_passenger_flow_bd
 from ${HIVE_DB_DWS}.f05_month_budget where type='project' group by the_month,projectguid
) t
on substr(a.the_date,1,7)=t.the_month and c.projectguid=t.projectguid
group by
a.the_date
,c.companyguid
,c.companyname
,c.projectguid
,c.projectname
,c.city
,c.province
,c.district_name
,k.projectopen_date
,c.is_ziguan
,c.is_finance
)
,sum_table as (
select
a.the_date
,a.week_eng_long_name as weekday
,b.projectguid
,b.projectname
,b.companyname
,b.companyguid
,b.city
,b.province
,b.district_name
,sum(case when a.the_date=b.the_date then b.jfmoney  else 0 end )as jfmoney
,sum(case when a.the_date=b.the_date then b.gxmoney else 0 end ) as gxmoney
,sum(case when a.the_date=b.the_date then b.jfcount  else 0 end ) as jfcount
,sum(case when a.the_date=b.the_date then b.gxcount  else 0 end ) as gxcount
,sum(case when a.the_date=b.the_date then b.amt_sum  else 0 end ) as amt_sum
,sum(case when a.the_date=date_add(b.the_date,7) then b.amt_sum else 0 end )as relative_amt_sum --环比当日营业额
,sum(case when a.the_date=add_months(b.the_date,12) then b.amt_sum else 0 end ) as last_year_amt_sum --同比当日营业额
,sum(case when a.the_date=b.the_date then b.num_sum else 0 end) as num_sum
,sum(case when a.the_date=b.the_date then b.bonus_asc else 0 end )as bonus_asc
,sum(case when a.the_date=b.the_date then b.bonus_inc else 0 end )as bonus_inc
,sum(case when a.the_date=b.the_date then b.member_saleamt else 0 end )as member_saleamt
,sum(case when a.the_date=b.the_date then b.member_salenum else 0 end )as member_salenum
,sum(case when a.the_date=b.the_date then b.customer else 0  end )as customer
,sum(case when a.the_date=date_add(b.the_date,7) then b.customer else 0 end )as relative_customer --环比客流
,sum(case when a.the_date=add_months(b.the_date,12) then b.customer else 0 end )as last_year_customer --同比客流
,sum(case when a.the_date=b.the_date then b.project_building_area else 0 end )as project_building_area
,sum(case when a.the_date=b.the_date then b.customer_density else 0 end )as customer_density
,sum(case when b.the_date=add_months(a.the_date,-12) then b.customer_density else 0 end )as last_year_customer_density --同比去年客流密度
,max(case when a.the_date=b.the_date then b.projectopen_date else null end) as projectopen_date
,max(case when a.the_date=b.the_date then b.is_ziguan else null end )as is_ziguan
,max(case when a.the_date=b.the_date then b.is_finance else null end )as is_finance
,sum(case when a.the_date=b.the_date then b.crent_area else 0 end )as crent_area
,sum(case when a.the_date=date_add(b.the_date,7) then b.crent_area else 0 end) as relative_crent_area --环比可出租面积
,sum(case when a.the_date=add_months(b.the_date,12) then b.crent_area else 0 end) as last_year_crent_area --同比可出租面积
,sum(case when a.the_date=b.the_date then b.rentarea else 0 end )as rentarea
,sum(case when a.the_date=date_add(b.the_date,7) then b.rentarea else 0 end) as relative_rentarea --环比已出租面积
,sum(case when a.the_date=add_months(b.the_date,12) then b.rentarea else 0 end )as last_year_rentarea --同比已出租面积
,sum(case when a.the_date=b.the_date then b.openarea else 0 end )as openarea
,sum(case when a.the_date=date_add(b.the_date,7) then b.openarea else 0 end )as relative_openarea --环比开业面积
,sum(case when a.the_date=add_months(b.the_date,12) then b.openarea else 0 end )as last_year_openarea --同比开业面积
,sum(case when a.the_date=b.the_date then b.jparea else 0  end) as jparea
,sum(case when a.the_date=date_add(b.the_date,7) then b.jparea else 0 end )as relative_jparea --环比接铺面积
,sum(case when a.the_date=add_months(b.the_date,12) then b.jparea else 0 end )as last_year_jparea --同比接铺面积
,sum(case when a.the_date=b.the_date then b.rent_area_bd else 0 end )as rent_area_bd_budget  --月预算可出租面积
,sum(case when a.the_date=b.the_date then b.sale_amt else 0 end) as sale_amt_budget  --月预算交易金额
,sum(case when a.the_date=b.the_date then b.budget_pingxiao else 0 end )as level_ground_effect_budget  --月预算坪效
,sum(case when a.the_date=b.the_date then b.average_passenger_flow_bd else 0 end) as average_passenger_flow_bd_budget  --预算日均客流
,sum(case when a.the_date=b.the_date then b.month_budget_insum else 0 end) as month_insum_budget  --月预算客流
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,7),'-01') then b.jfmoney else 0 end ) as month_jfmoney
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,4),'-01-01') then b.jfmoney else 0 end ) as year_jfmoney
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,7),'-01') then b.gxmoney else 0 end ) as month_gxmoney
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,4),'-01-01') then b.gxmoney else 0 end ) as year_gxmoney
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,7),'-01') then b.jfcount else 0 end ) as month_jfcount
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,4),'-01-01') then b.jfcount else 0 end ) as year_jfcount
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,7),'-01') then b.gxcount else 0 end ) as month_gxcount
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,4),'-01-01') then b.gxcount else 0 end ) as year_gxcount
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,7),'-01') then b.amt_sum else 0 end ) as month_amt_sum  --月累交易金额
,sum(case when date_sub(a.the_date,7)>=b.the_date and b.the_date>=concat(substr(date_sub(a.the_date,7),1,7),'-01') then b.amt_sum else 0 end ) as relative_month_amt_sum --环比月累营业额
,sum(case when add_months(a.the_date,-12)>=b.the_date and b.the_date>=concat(substr(add_months(a.the_date,-12),1,7),'-01') then b.amt_sum else 0 end ) as last_year_month_amt_sum --同比月累营业额
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,4),'-01-01') then b.amt_sum else 0 end ) as year_amt_sum
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,7),'-01') then b.num_sum else 0 end ) as month_num_sum
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,4),'-01-01') then b.num_sum else 0 end ) as year_num_sum
,sum(case when a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,7),'-01') then b.customer else 0 end ) as month_customer
,sum(case when add_months(a.the_date,-12)>=b.the_date and b.the_date>=concat(substr(add_months(a.the_date,-12),1,7),'-01') then b.customer else 0 end ) as last_year_month_customer --同期月累客流
,(sum(case when b.projectopen_date <=a.the_date  and  a.the_date>=b.the_date and b.the_date>=concat(substr(a.the_date,1,7),'-01') then b.amt_sum else 0 end)
/sum(case when a.the_date=b.the_date then b.crent_area else 0 end )
/case when b.projectopen_date>=concat(substr(a.the_date,1,7),'-01') and b.projectopen_date <=a.the_date then datediff(a.the_date,substr(b.projectopen_date,1,10)) else datediff(a.the_date,concat(substr(a.the_date,1,7),'-01'))+1  end )*30 as  m_level_ground_effect --月累坪效

,(sum(case when b.projectopen_date <=add_months(a.the_date,-12)  and  add_months(a.the_date,-12)>=b.the_date and b.the_date>=concat(substr(add_months(a.the_date,-12),1,7),'-01') then b.amt_sum else 0 end)
/sum(case when add_months(a.the_date,-12)=b.the_date then b.crent_area else 0 end )
/case when b.projectopen_date>=concat(substr(add_months(a.the_date,-12),1,7),'-01') and b.projectopen_date <=add_months(a.the_date,-12) then datediff(add_months(a.the_date,-12),substr(b.projectopen_date,1,10)) else datediff(add_months(a.the_date,-12),concat(substr(add_months(a.the_date,-12),1,7),'-01'))+1 end )*30 as  last_year_m_level_ground_effect --同比月累坪效
from (
select
the_date
,week_eng_long_name
from ${HIVE_DB_DIM}.dim_date
where the_date>=date_sub('${START_DATEKEY}',4)
and  the_date<'${END_DATEKEY}'
group by the_date,week_eng_long_name
) a
left join dim_main b on 1=1
where a.the_date>=b.the_date
group by
a.the_date
,a.week_eng_long_name
,b.projectguid
,b.projectname
,b.companyname
,b.companyguid
,b.city
,b.province
,b.district_name
,b.projectopen_date
)


insert overwrite table a05_receipt_sale_amt partition(date_key)
select
 a.the_date           as the_date
,a.weekday            as weekday
,a.companyguid        as companyguid
,a.companyname        as companyname
,a.projectguid        as projectguid
,a.projectname        as projectname
,a.city               as city
,a.province           as province
,a.district_name      as district_name
,case when add_months(a.the_date,12)>=a.projectopen_date and a.projectopen_date>a.the_date then 1
       when a.projectopen_date<=a.the_date then 0
       else 2
       end as preparatory_period  --是否筹备期
,a.jfmoney                           as  jfmoney
,a.month_jfmoney                     as  month_jfmoney
,a.year_jfmoney                      as  year_jfmoney
,a.gxmoney                           as  gxmoney
,a.month_gxmoney                     as  month_gxmoney
,a.year_gxmoney                      as  year_gxmoney
,a.jfcount                           as  jfcount
,a.month_jfcount                     as  month_jfcount
,a.year_jfcount                      as  year_jfcount
,a.gxcount                           as  gxcount
,a.month_gxcount                     as  month_gxcount
,a.year_gxcount                      as  year_gxcount
,a.amt_sum                           as  amt_sum
,a.relative_amt_sum                  as  relative_amt_sum
,a.last_year_amt_sum                 as  last_year_amt_sum
,a.month_amt_sum                     as  month_amt_sum
,a.relative_month_amt_sum            as  relative_month_amt_sum
,a.last_year_month_amt_sum           as  last_year_month_amt_sum
,a.year_amt_sum                      as  year_amt_sum
,a.num_sum                           as  num_sum
,a.month_num_sum                     as  month_num_sum
,a.year_num_sum                      as  year_num_sum
,a.bonus_asc                         as  bonus_asc
,a.bonus_inc                         as  bonus_inc
,a.member_saleamt                    as  member_saleamt
,a.member_salenum                    as  member_salenum
,a.customer                          as  customer
,a.relative_customer                 as  relative_customer
,a.last_year_customer                as  last_year_customer
,a.month_customer                    as  month_customer
,a.last_year_month_customer          as  last_year_month_customer
,a.customer_density                  as  customer_density
,a.last_year_customer_density        as  last_year_customer_density
,a.project_building_area             as  project_building_area
,a.is_ziguan                         as  is_ziguan
,a.is_finance                        as  is_finance
,a.crent_area                        as  crent_area
,a.relative_crent_area               as  relative_crent_area
,a.last_year_crent_area              as  last_year_crent_area
,a.rentarea                          as  rentarea
,a.relative_rentarea                 as  relative_rentarea
,a.last_year_rentarea                as  last_year_rentarea
,a.openarea                          as  openarea
,a.relative_openarea                 as  relative_openarea
,a.last_year_openarea                as  last_year_openarea
,a.jparea                            as  jparea
,a.relative_jparea                   as  relative_jparea
,a.last_year_jparea                  as  last_year_jparea
,a.m_level_ground_effect             as  m_level_ground_effect
,a.last_year_m_level_ground_effect   as  last_year_m_level_ground_effect
,a.rent_area_bd_budget               as  rent_area_bd_budget
,a.sale_amt_budget                   as  sale_amt_budget
,a.level_ground_effect_budget        as  level_ground_effect_budget
,a.average_passenger_flow_bd_budget  as  average_passenger_flow_bd_budget
,a.month_insum_budget                as  month_insum_budget
,b.stationnum                        as  stationnum
,c.traffice_flow                     as  traffice_flow
,from_unixtime(unix_timestamp(),'yyyy-MM-dd HH:mm:ss') as op_time
,'${EXECUTION_ID}'as execution_id
,'${CURRENT_FLOW_START_DAY}'  as load_date
,substr(the_date,1,7) as date_key
from sum_table a
left join ${HIVE_DB_DWD_DIM}.dim_smartbi_project_station b
on a.projectname=b.projectname
left join ${HIVE_DB_DWD_DIM}.dim_traffic_flow  c
on a.projectguid=c.project_id
and a.the_date=c.biz_date
union all
select
 a.the_date                          as the_date
,a.weekday                          as weekday
,a.companyguid                      as companyguid
,a.companyname                      as companyname
,a.projectguid                      as projectguid
,a.projectname                      as projectname
,a.city                             as city
,a.province                         as province
,a.district_name                    as district_name
,a.preparatory_period               as preparatory_period
,a.jfmoney                          as jfmoney
,a.month_jfmoney                    as month_jfmoney
,a.year_jfmoney                     as year_jfmoney
,a.gxmoney                          as gxmoney
,a.month_gxmoney                    as month_gxmoney
,a.year_gxmoney                     as year_gxmoney
,a.jfcount                          as jfcount
,a.month_jfcount                    as month_jfcount
,a.year_jfcount                     as year_jfcount
,a.gxcount                          as gxcount
,a.month_gxcount                    as month_gxcount
,a.year_gxcount                     as year_gxcount
,a.amt_sum                          as amt_sum
,a.relative_amt_sum                 as relative_amt_sum
,a.last_year_amt_sum                as last_year_amt_sum
,a.month_amt_sum                    as month_amt_sum
,a.relative_month_amt_sum           as relative_month_amt_sum
,a.last_year_month_amt_sum          as last_year_month_amt_sum
,a.year_amt_sum                     as year_amt_sum
,a.num_sum                          as num_sum
,a.month_num_sum                    as month_num_sum
,a.year_num_sum                     as year_num_sum
,a.bonus_asc                        as bonus_asc
,a.bonus_inc                        as bonus_inc
,a.member_saleamt                   as member_saleamt
,a.member_salenum                   as member_salenum
,a.customer                         as customer
,a.relative_customer                as relative_customer
,a.last_year_customer               as last_year_customer
,a.month_customer                   as month_customer
,a.last_year_month_customer         as last_year_month_customer
,a.customer_density                 as customer_density
,a.last_year_customer_density       as last_year_customer_density
,a.project_building_area            as project_building_area
,a.is_ziguan                        as is_ziguan
,a.is_finance                       as is_finance
,a.crent_area                       as crent_area
,a.relative_crent_area              as relative_crent_area
,a.last_year_crent_area             as last_year_crent_area
,a.rentarea                         as rentarea
,a.relative_rentarea                as relative_rentarea
,a.last_year_rentarea               as last_year_rentarea
,a.openarea                         as openarea
,a.relative_openarea                as relative_openarea
,a.last_year_openarea               as last_year_openarea
,a.jparea                           as jparea
,a.relative_jparea                  as relative_jparea
,a.last_year_jparea                 as last_year_jparea
,a.m_level_ground_effect            as m_level_ground_effect
,a.last_year_m_level_ground_effect  as last_year_m_level_ground_effect
,a.rent_area_bd_budget              as rent_area_bd_budget
,a.sale_amt_budget                  as sale_amt_budget
,a.level_ground_effect_budget       as level_ground_effect_budget
,a.average_passenger_flow_bd_budget as average_passenger_flow_bd_budget
,a.month_insum_budget               as month_insum_budget
,b.stationnum                     as  stationnum
,c.traffice_flow                  as  traffice_flow
,a.op_time
,a.execution_id
,a.load_date
,a.date_key
from ${HIVE_DB_APP}.a05_receipt_sale_amt  a
left join ${HIVE_DB_DWD_DIM}.dim_smartbi_project_station b
on a.projectname=b.projectname
left join ${HIVE_DB_DWD_DIM}.dim_traffic_flow  c
on a.projectguid=c.project_id
and a.the_date=c.biz_date

where a.the_date<date_sub('${START_DATEKEY}',4) and substr(a.the_date,1,7)>=substr(date_sub('${START_DATEKEY}',4),1,7)
;
"
task_incre_type=multi-day

