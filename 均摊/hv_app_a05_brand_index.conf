sql="
use ${HIVE_DB_APP};
set hive.exec.dynamic.partition.mode=nonstrict;
set hive.exec.parallel=true;
set hive.exec.parallel.thread.number=8;
set mapred.reduce.tasks = 15;
set mapreduce.map.memory.mb=4096;
set mapreduce.reduce.memory.mb=4096;
set hive.exec.reducers.bytes.per.reducer=500000000;
insert overwrite table ${HIVE_DB_APP}.a05_brand_index partition (datekey)
select 
 a.the_month                  --年月
,a.projectguid                --项目id
,a.contractid                 --合同ID
,a.contractno                 --合同类型
,a.rentaltype                 --出租类型
,b.format_id                  --业态id
,b.brand_name                 --品牌名称
,c.city                       --城市名
,c.province                   --省份名
,c.district_name              --片区名称
,c.companyid                  --地区id
,c.companyguid                --地区guid
,c.companyname                --地区名称
,c.projectopen_date           --项目开业日期
,case when substr(add_months(concat(a.the_month,'-01'),12 ),1,7)>=substr(c.projectopen_date,1,7)  and a.the_month<substr(c.projectopen_date,1,7) then '1' when  a.the_month>=substr(c.projectopen_date,1,7) then '0' else '2' end  as is_preparatory_period              --是否筹备期
,case when substr(add_months(c.projectopen_date,12),1,7)<=a.the_month then '1' else '0' end as is_storage_project  --是否存量项目
,null
,null
,null
,null
,sum(a.saleamt)/b.brand_num              as    saleamt               --  营业额
,sum(e.bg_saleamt)/b.brand_num           as    bg_saleamt           --预算营业额                
,sum(a.saleamt_num)/b.brand_num          as    saleamt_num          --交易次数（营业笔数）
,sum(e.mng_fee)/b.brand_num              as    mng_fee              --预算推广服务费              
,sum(e.rentfee)/b.brand_num              as    rentfee              --预算保底租金               
,sum(e.promotivefee)/b.brand_num         as    promotivefee         --物业管理费                
,sum(e.revenufee)/b.brand_num            as    revenufee            --预算抽成租金                          
,sum(a.bd_income_y)/b.brand_num          as    bd_income_y          --收入_含税保底租金收入          
,sum(a.taking_income_y)/b.brand_num      as    taking_income_y      --收入_含税抽成租金收入          
,sum(a.mng_income_y)/b.brand_num         as    mng_income_y         --收入_含税物业管理费收入         
,sum(a.tg_income_y)/b.brand_num          as    tg_income_y          --收入_含税推广服务费收入         
,sum(a.bd_income_n)/b.brand_num          as     bd_income_n         --收入_不含税保底租金收入         
,sum(a.taking_income_n)/b.brand_num      as    taking_income_n      --收入_不含税抽成租金收入         
,sum(a.mng_income_n)/b.brand_num         as    mng_income_n         --收入_不含税物业管理费收入        
,sum(a.tg_income_n)/b.brand_num          as    tg_income_n          --收入_不含税推广服务费收入        
,sum(case when a.is_jprent = '1' then a.totalarea else 0 end)/b.brand_num as totalarea             --接铺面积 
,sum(case when a.is_yrent  = '1' then a.totalarea else 0 end)/b.brand_num as project_rent_area     --可出租面积
,from_unixtime(unix_timestamp(), 'yyyy-MM-dd HH:mm:ss')       as op_time                   --时间戳
,'${EXECUTION_ID}'                                            as execution_id              --批次号
,'${CURRENT_FLOW_START_DAY}'                                  as load_date                 --入库时间
,a.the_month as datekey
from  ${HIVE_DB_APP}.a05_month_contract a --月合同表

left join  (select   a.*,b.brand_name,b.format_id    from 
                     (select DISTINCT 
                      t.contractid
                     ,case when t2.newbrandid='-' then t2.brand_id else t2.newbrandid end    as brandid
                     ,sum(1) OVER (PARTITION by t.contractid)  as brand_num
                     FROM ${HIVE_DB_DWD_SYSMARTBI}.sysmartbi_rt_contract t
                     left join ${HIVE_DB_DWD_SYSMARTBI}.sysmartbi_rt_contractbrand t1 on t1.contractid = t.contractid
                     left join ${HIVE_DB_DIM}.d05_longfor_brand t2 
                     on  t2.brand_id = t1.brandid 
                     where t1.status = 'ACTIVE'
                     and t2.status <> 'INACTIVE')a
            left join ${HIVE_DB_DIM}.d05_longfor_brand b on a.brandid=b.brand_id
             ) b 
             on a.contractid=b.contractid
left join  ${HIVE_DB_DIM}.pub_longfor_project_temp_sysmartbi c
on a.projectguid=c.projectguid
left join (
select
 a.contractid
,b.version_code
,a.the_month
,sum(b.sale_amt)  as bg_saleamt --预算营业额
,sum(b.mng_fee)   as mng_fee
,sum(b.rentfee)   as rentfee
,sum(b.promotivefee)  as promotivefee
,sum(b.revenufee)   as revenufee
,count(1)             as unit_num
 from  ${HIVE_DB_APP}.a05_month_unit a
 left join ${HIVE_DB_DWS}.f05_month_budget b
 on a.the_month=b.the_month
 where a.unittype in ('MainShop','SpecialShop','Unittype1')
  and a.unitid=b.unitid
  and b.type='unit'
  and b.version_code='initversion'
  group by a.contractid
            ,b.version_code
            ,a.the_month
   ) e
on a.the_month = e.the_month
and a.contractid=e.contractid
where a.rentaltype in ('Main','Exclusive','SubAnchor','Site','SingleCabinet') --主力店租赁合同,专卖店租赁合同,次主力店租赁合同,单贵机租赁
and   (a.is_jprent ='1' or a.saleamt > 0 )
and   a.the_month<=substr('${END_DATEKEY}',1,7) 
and   a.the_month>=substr(add_months('${START_DATEKEY}',-4),1,7)
group by
a.the_month                 --年月
,a.projectguid              --项目id
,b.format_id                --业态id            
,b.brandid
,b.brand_name               --品牌名称   
,b.brand_num
,c.city                     --城市名
,c.province                 --省份名
,c.district_name            --片区名称
,c.companyid                --地区id
,c.companyguid              --地区guid
,c.companyname              --地区名称
,c.projectopen_date         --项目开业日期
,a.contractid                 --合同ID
,a.contractno                 --合同类型
,a.rentaltype                 --出租类型
"
task_incre_type=multi-day

